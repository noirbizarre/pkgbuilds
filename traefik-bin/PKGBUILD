# Maintainer: Axel Haustant

_pkgname='traefik'
pkgname="${_pkgname}-bin"
pkgver=2.2.1
pkgrel=1
pkgdesc='A modern reverse proxy'
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/containous/traefik'
license=('MIT')
provides=('traefik')
conflicts=('traefik')
optdepends=('docker: docker backend support')
backup=(
  'etc/traefik/traefik.yml'
  'etc/logrotate.d/traefik'
)
source=(
  'traefik.logrotate'
  'traefik.service'
  'traefik.sysusers'
  'traefik.tmpfiles'
  'traefik.yml'
)
sha256sums=('da59d047177cae773d248684e8484d91c71201f5bc07fe72452efdf9bed1a7f4'
            '077df184d627bf8cae608bf57db9d3a1331d7eeeee910370f08f90ea928b1b3e'
            '4654ef0468251bcdf814007d5b435654b8db3e6ebaf1a279bcd4f451378d5d86'
            'b39e7163550cf9069cbb5fb9186c582c134eb2d06037c6a8845de038591346c0'
            '9ce657f898c40f3852fb5ba2b00221735281dff453400a4bfef278bfa86be3d8')
sha256sums_i686=('757153997fb9b2a2a3004edfb86a9ad2a582809a198457b9ab4f2c238c264583')
sha256sums_x86_64=('04139683e0cd6fc4e98eae9d469490d1a85074a0d810f640296f81991763657c')
sha256sums_armv6h=('0b49c8c1b089b2b2096ceb3b730bd8eb1ef45a37a05f15b8f45bd6ba0f063ab5')
sha256sums_armv7h=('012778da71c29a0dd9275c6981e00ffad4bb198f21c7d7c23c55e2620d6fbb50')
sha256sums_aarch64=('4a4ad942bb5fa21ee6978e27fe5e2125ba7ecf222eac3ca69e6ad958234c4a37')
source_i686=("${_pkgname}-i686-${pkgver}.tar.gz"::"https://github.com/containous/${_pkgname}/releases/download/v${pkgver}/${_pkgname}_v${pkgver}_linux_386.tar.gz")
source_x86_64=("${_pkgname}-x86_64-${pkgver}.tar.gz"::"https://github.com/containous/${_pkgname}/releases/download/v${pkgver}/${_pkgname}_v${pkgver}_linux_amd64.tar.gz")
source_armv6h=("${_pkgname}-armv6h-${pkgver}.tar.gz"::"https://github.com/containous/${_pkgname}/releases/download/v${pkgver}/${_pkgname}_v${pkgver}_linux_armv6.tar.gz")
source_armv7h=("${_pkgname}-armv7h-${pkgver}.tar.gz"::"https://github.com/containous/${_pkgname}/releases/download/v${pkgver}/${_pkgname}_v${pkgver}_linux_armv7.tar.gz")
source_aarch64=("${_pkgname}-aarch64-${pkgver}.tar.gz"::"https://github.com/containous/${_pkgname}/releases/download/v${pkgver}/${_pkgname}_v${pkgver}_linux_arm64.tar.gz")

prepare() {
  mkdir -p "${srcdir}/${pkgname}-${pkgver}"
  tar xvzf "${srcdir}/${_pkgname}-${CARCH}-${pkgver}.tar.gz" -C "${srcdir}/${pkgname}-${pkgver}"
}

package() {
  install -Dm644 "${srcdir}/traefik.sysusers" "${pkgdir}/usr/lib/sysusers.d/traefik.conf"
  install -Dm644 "${srcdir}/traefik.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/traefik.conf"

  install -Dm644 "${srcdir}/traefik.logrotate" "${pkgdir}/etc/logrotate.d/traefik"
  install -Dm644 "${srcdir}/traefik.service" "${pkgdir}/usr/lib/systemd/system/traefik.service"
  install -Dm644 "${srcdir}/traefik.yml" "${pkgdir}/etc/traefik/traefik.yml"
  install -dm755 "${pkgdir}/etc/traefik/conf.d"

  install -Dm644 "${srcdir}/LICENSE.md" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm755 "${srcdir}/traefik" "${pkgdir}/usr/bin/${_pkgname}"
}
